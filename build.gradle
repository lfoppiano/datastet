buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        flatDir {
            dirs 'localLibs'
        }
        maven {
            name = 'ajoberstar-backup'
            url = 'https://ajoberstar.org/bintray-backup/'
        }
    }
    dependencies {
        classpath 'gradle.plugin.org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.12.0'
        classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0"
        classpath 'com.adarshr:gradle-test-logger-plugin:2.0.0'
        classpath group: 'org.yaml', name: 'snakeyaml', version: '1.19'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'org.ajoberstar.grgit' version '5.3.0' apply false
    id 'distribution'
    id 'application'
}

apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.8'
}

apply plugin: 'java-library'
apply plugin: 'base'
apply plugin: 'com.github.kt3k.coveralls'

group = "org.grobid.datastet"
version = '0.8.1'

description = """datastet"""

import org.apache.tools.ant.taskdefs.condition.Os

sourceCompatibility = 1.11
targetCompatibility = 1.11

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

def conf = new org.yaml.snakeyaml.Yaml().load(new File("resources/config/config.yml").newInputStream())
def grobidHome = conf.grobidHome.replace("\$", "").replace('{', "").replace("GROBID_HOME:- ", "").replace("}", "")
if (grobidHome.startsWith("../")) {
    grobidHome = "${rootProject.rootDir}/${grobidHome}"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url new File(rootProject.rootDir, "localLibs")
    }
    maven {
        name = 'ajoberstar-backup'
        url = 'https://ajoberstar.org/bintray-backup/'
    }
//    flatDir {
//        dirs 'localLibs'
//    }
}

ext {
    // treating them separately, these jars will be flattened into grobid-core.jar on installing,
    // to avoid missing dependencies from the projects that include grobid-core (see 'jar' taskin grobid-core)
    localLibs = []
}

dependencies {

    // packaging local libs inside output jar
    //implementation fileTree(dir: new File(rootProject.rootDir, 'lib'), include: localLibs)

    implementation group: 'net.arnx', name: 'jsonic', version: '1.3.10'

    //Logging
    implementation 'org.slf4j:slf4j-api:1.7.30'

    //Apache commons
    implementation group: 'commons-pool', name: 'commons-pool', version: '1.6'
    implementation group: 'commons-io', name: 'commons-io', version: '2.9.0'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.3'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.5'

    //Dropwizard
    implementation "io.dropwizard:dropwizard-core:1.3.23"
    implementation "io.dropwizard:dropwizard-assets:1.3.23"
    implementation "com.hubspot.dropwizard:dropwizard-guicier:1.3.5.0"
    implementation "io.dropwizard:dropwizard-testing:1.3.23"
    implementation "io.dropwizard:dropwizard-forms:1.3.23"
    implementation "io.dropwizard:dropwizard-client:1.3.23"
    implementation "io.dropwizard:dropwizard-auth:1.3.23"
    implementation "io.dropwizard.metrics:metrics-core:4.0.0"
    implementation "io.dropwizard.metrics:metrics-servlets:4.0.0"

    implementation group: 'com.google.guava', name: 'guava', version: '28.2-jre'

    //Parsing xml/json
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.10.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.10.1'
    implementation group: 'xom', name: 'xom', version: '1.3.2'
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.0'

    implementation group: 'javax.activation', name: 'activation', version: '1.1.1'
    implementation group: 'asm', name: 'asm', version: '3.3.1'

    implementation 'black.ninia:jep:4.0.2'
    // for sentence segmentation only
    implementation 'org.apache.opennlp:opennlp-tools:1.9.1'
    implementation "joda-time:joda-time:2.9.9"
    implementation "org.apache.lucene:lucene-analyzers-common:4.5.1"
    implementation group: 'org.jruby', name: 'jruby-complete', version: '9.2.13.0'
    implementation "org.apache.pdfbox:pdfbox:2.0.18"

    implementation group: 'org.semanticweb.yars', name: 'nxparser-parsers', version: '2.3.3'
    implementation group: 'org.jvnet', name: 'mimepull', version: '1.6'

    //Grobid
    implementation(group: 'org.grobid', name: 'grobid-core', version: '0.8.1') {
        exclude group: 'org.slf4j', module: "slf4j-log4j12"
    }
    implementation(group: 'org.grobid', name: 'grobid-trainer', version: '0.8.1') {
        exclude group: 'org.slf4j', module: "slf4j-log4j12"
    }

    //Tests
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.powermock:powermock-api-easymock:2.0.9'
}

configurations.all {
    resolutionStrategy {
        force 'xml-apis:xml-apis:1.4.01'
    }

    exclude group: 'org.slf4j', module: "slf4j-log4j12"
}

def getJavaLibraryPath = {
    def jepLocalLibraries = ""
    if (Os.isFamily(Os.FAMILY_MAC)) {
        if (Os.OS_ARCH.equals("aarch64")) {
            jepLocalLibraries = "${file("${grobidHome}/lib/mac_arm-64").absolutePath}"
        } else {
            jepLocalLibraries = "${file("${grobidHome}/lib/mac-64").absolutePath}"
        }
    } else if (Os.isFamily(Os.FAMILY_UNIX)) {
        jepLocalLibraries = "${grobidHome}/lib/lin-64/jep:${grobidHome}/lib/lin-64"
    } else {
        throw new RuntimeException("Unsupported platform!")
    }

    def javaLibraryPath = "${System.getProperty('java.library.path')}:${jepLocalLibraries}"

    def envPrefix = ""
    if (System.env.CONDA_PREFIX) {
        envPrefix = "${System.env.CONDA_PREFIX}"
    } else if (System.env.VIRTUAL_ENV) {
        envPrefix = "${System.env.VIRTUAL_ENV}"
    }

    if (envPrefix) {
        def envLibs = "${envPrefix}/lib"
        def pythonDirectory = file(envLibs).listFiles({ it.toString().contains("/lib/python") } as FileFilter)?.first()
        def pythonVersion = (pythonDirectory =~ /python([0-9]\.[0-9]+)/)[0][1]

        javaLibraryPath += ":${envLibs}:${envLibs}/python${pythonVersion}/site-packages/jep"
    }
    return javaLibraryPath
}

test {
    testLogging.showStandardStreams = true
    exclude '**/**IntegrationTest**'

    if (JavaVersion.current().compareTo(JavaVersion.VERSION_1_8) > 0) {
        jvmArgs "--add-opens", "java.base/java.util.stream=ALL-UNNAMED",
            "--add-opens", "java.base/java.io=ALL-UNNAMED"
    }
    systemProperty "java.library.path", "${System.getProperty('java.library.path')}:" + getJavaLibraryPath()
}

// return the default value if the property has not been specified in command line
ext.getArg = { propName, defaultVal ->
    return project.hasProperty(propName) ? project.getProperty(propName) : defaultVal;
}

task integration(type: Test) {
    include '**'
    maxHeapSize = "1024m"

    if (JavaVersion.current().compareTo(JavaVersion.VERSION_1_8) > 0) {
        jvmArgs "--add-opens", "java.base/java.util.stream=ALL-UNNAMED",
                "--add-opens", "java.base/java.io=ALL-UNNAMED"
    }
    systemProperty "java.library.path", "${System.getProperty('java.library.path')}:" + getJavaLibraryPath()
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

apply plugin: 'java'
apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}

task install(dependsOn: publishToMavenLocal)

task mainJar(type: ShadowJar) {
    zip64 true
    from sourceSets.main.output

    from {
        project.configurations.runtimeClasspath.collect {
            it.isDirectory() ? [] : localLibs.contains(it.getName()) ? zipTree(it) : []
        }
    }

    exclude("logback.xml")

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

shadowJar {
    archiveClassifier = 'onejar'
    mergeServiceFiles()
    zip64 true
    manifest {
        attributes 'Main-Class': 'org.grobid.core.main.batch.DatastetMain'
    }
    //from sourceSets.main.output

    from {
        project.configurations.runtimeClasspath.collect {
            it.isDirectory() ? [] : localLibs.contains(it.getName()) ? zipTree(it) : []
        }
    }

    exclude("logback.xml")

    configurations = [project.configurations.runtimeClasspath]

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jar {
    dependsOn mainJar
    enabled true
}

distZip.enabled = true
distTar.enabled = false
shadowDistZip.enabled = false
shadowDistTar.enabled = false

artifacts {
    archives jar
    archives shadowJar
}

import org.ajoberstar.grgit.Grgit

task downloadModelsGit {
    doLast {
        if (new File("${grobidHome}/models/datastet").exists()) {
            def grgit = Grgit.open(
                dir: "${grobidHome}/models/datastet",
            )
            grgit.pull()
        } else {
            def grgit = Grgit.clone(
                dir: "${grobidHome}/models/datastet",
                uri: "https://huggingface.co/sciencialab/datastet-models",
                depth: 1
            )
        }

    }
}

task installModels(dependsOn: [downloadModelsGit]){
    doLast {
        def sourceDir = file("${grobidHome}/models/datastet")
        def destDir = file("${grobidHome}/models/")

        Arrays.asList(
            "context_bert",
            "context_creation_bert",
            "context_shared_bert",
            "context_used_bert",
            "datasets-BERT_CRF",
            "dataseer",
            "dataseer-binary_bert",
            "dataseer-first_bert",
            "dataseer-reuse_bert"
        ).each { String subDirName ->
            var subDir = new File(sourceDir, subDirName)
            if (subDir.isDirectory()) {
                copy {
                    from subDir
                    into new File(destDir, subDirName)
                }
            }
        }

        delete files("${sourceDir}")
    }
}

wrapper {
    gradleVersion "7.2"
}

// Custom Tasks

///////////////////////////
// train and eval ML models

// Run like this: ./gradlew train_dataseer -PgH=/path/grobid/home
task(train_dataseer, dependsOn: 'classes', type: JavaExec, group: 'training') {
    mainClass = 'org.grobid.trainer.DataseerTrainerRunner'
    classpath = sourceSets.main.runtimeClasspath
    args '0', 'dataseer', getArg('gH', '../grobid-home'), getArg('t', '0')
    if (JavaVersion.current().compareTo(JavaVersion.VERSION_1_8) > 0) {
        jvmArgs '-Xmx3072m', "--add-opens", "java.base/java.lang=ALL-UNNAMED"
    } else {
        jvmArgs '-Xmx3072m'
    }
    systemProperty "java.library.path", "${System.getProperty('java.library.path')}:" + getJavaLibraryPath()
}

// Run like this: ./gradlew train_dataseer -PgH=/path/grobid/home
//task(train_dataseer, dependsOn: 'classes', type: JavaExec, group: 'training') {
//    mainClass = 'org.grobid.trainer.DataseerTrainer'
//    classpath = sourceSets.main.runtimeClasspath
//    args '0', 'dataseer', getArg('gH', '../grobid-home'), getArg('b', ''),
//    jvmArgs '-Xms2g', '-Xmx8g'
//}

// Run like this: ./gradlew eval_dataseer -PgH=/path/grobid/home
task(eval_dataseer, dependsOn: 'classes', type: JavaExec, group: 'evaluation') {
    mainClass = 'org.grobid.trainer.DataseerTrainerRunner'
    classpath = sourceSets.main.runtimeClasspath
    args '1', 'dataseer', getArg('gH', '../grobid-home')
    if (JavaVersion.current().compareTo(JavaVersion.VERSION_1_8) > 0) {
        jvmArgs '-Xmx3072m', "--add-opens", "java.base/java.lang=ALL-UNNAMED"
    } else {
        jvmArgs '-Xmx3072m'
    }
    systemProperty "java.library.path", "${System.getProperty('java.library.path')}:" + getJavaLibraryPath()
//    jvmArgs '-Xms2g', '-Xmx8g'
}

// Run like this: ./gradlew eval_dataseer_split -PgH=/path/grobid/home -Ps=0.8 -Pt=10
task(eval_dataseer_split, dependsOn: 'classes', type: JavaExec, group: 'evaluation') {
    mainClass = 'org.grobid.trainer.DataseerTrainerRunner'
    classpath = sourceSets.main.runtimeClasspath
    args '2', 'dataseer', getArg('gH', '../grobid-home'), getArg('s', 0.9), getArg('t', '0')
    //jvmArgs '-Xms2g', '-Xmx8g'
    if (JavaVersion.current().compareTo(JavaVersion.VERSION_1_8) > 0) {
        jvmArgs '-Xms2g', '-Xmx8g', "--add-opens", "java.base/java.lang=ALL-UNNAMED"
    } else {
        jvmArgs '-Xms2g', '-Xmx8g'
    }
    systemProperty "java.library.path", "${System.getProperty('java.library.path')}:" + getJavaLibraryPath()
}

// Run like this: ./gradlew eval_dataseer_nfold -PgH=/path/grobid/home -Pt=10
task(eval_dataseer_nfold, dependsOn: 'classes', type: JavaExec, group: 'evaluation') {
    mainClass = 'org.grobid.trainer.DataseerTrainerRunner'
    classpath = sourceSets.main.runtimeClasspath
    args '3', 'dataseer', getArg('gH', '../grobid-home'), getArg('n', 10), getArg('t', '0')
    if (JavaVersion.current().compareTo(JavaVersion.VERSION_1_8) > 0) {
        jvmArgs '-Xms2g', '-Xmx8g', "--add-opens", "java.base/java.lang=ALL-UNNAMED"
    } else {
        jvmArgs '-Xms2g', '-Xmx8g'
    }
    systemProperty "java.library.path", "${System.getProperty('java.library.path')}:" + getJavaLibraryPath()
}

// Run like this: ./gradlew annotated_corpus_generator_csv -Pfull=/path/input/fulltext -Ppdf=/path/input/pdf -Pcsv=/path/csv -Pxml=/output/directory
task(annotated_corpus_generator_csv, dependsOn: 'classes', type: JavaExec, group: 'import') {
    mainClass = 'org.grobid.trainer.AnnotatedCorpusGeneratorCSV'
    classpath = sourceSets.main.runtimeClasspath
    args getArg('full', ''), getArg('pdf', ''), getArg('csv', ''), getArg('xml', 'resources/dataset/dataseer/corpus/')
    if (JavaVersion.current().compareTo(JavaVersion.VERSION_1_8) > 0) {
        jvmArgs '-Xms2g', '-Xmx8g', "--add-opens", "java.base/java.lang=ALL-UNNAMED"
    } else {
        jvmArgs '-Xms2g', '-Xmx8g'
    }
    systemProperty "java.library.path", "${System.getProperty('java.library.path')}:" + getJavaLibraryPath()
}

//////////
// service
apply plugin: 'application'
application {
    mainClassName = 'org.grobid.service.DatastetApplication'

    run {
        if (JavaVersion.current().compareTo(JavaVersion.VERSION_1_8) > 0) {
            jvmArgs "--add-opens", "java.base/java.lang=ALL-UNNAMED"
        }
        workingDir = rootProject.rootDir
        systemProperty "java.library.path", "${System.getProperty('java.library.path')}:" + getJavaLibraryPath()

        args = ['server', 'resources/config/config.yml']
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
    dependsOn test // tests are required to run before generating the report
}

coveralls {
    jacocoReportPath 'build/reports/jacoco/test/jacocoTestReport.xml'
}
